:toc:
= Architecture Linting Report

[[default]]
[role=group,includesGroups=,includesGroups="structure:Default"]

include::/structure.adoc[]

== Default Rules

This section describes that default rules that are executed during each build.

- <<structure:Default>>

[[structure:Default]]
[role=group,includesConcepts="arch:markAdapterLayer,arch:markServiceLayer,arch:markDomainLayer,arch:markDomains,arch:markServices,arch:markAdapters",includesConstraints="arch:detectIllegalLayerDependencies,arch:detectIllegalDomainDependencies,arch:detectIllegalAdapterDependencies,arch:detectIllegalCommonDependencies"]
== Structure

[[arch:markTopLevelLayer]]
[source,cypher,role=concept]
.Label the top level packages with the label `ToplevelLayer` and `HexArch`
----
MATCH (package:Java:Package)
WHERE
  package.fqn =~ 'corp\\.fantastic\\.[a-z][a-z0-9]*\\.[a-z][a-z0-9]*'
AND
  package.name <> "configurations"
SET package:HexArch:ToplevelLayer
RETURN package
----

[[arch:markAdapterLayer]]
[source,cypher,role=concept,requiresConcepts="arch:markTopLevelLayer"]
.Label the layer for the adapters with the label `Adapters`
----
MATCH (package:Java:Package:ToplevelLayer)
WHERE package.name ='adapters'
SET package:Adapters
RETURN package
----

[[arch:markAdapters]]
[source,cypher,role=concept,requiresConcepts="arch:markAdapterLayer"]
.Labels each single adapter with the label `Adapter`
----
MATCH (adapterLayer:Adapters:HexArch)-[:CONTAINS]->(adapter:Package:Java)
WHERE adapter.name <> "common"
SET adapter:Adapter
return adapter
----

[[arch:markServiceLayer]]
[source,cypher,role=concept,requiresConcepts="arch:markTopLevelLayer"]
.Label  the layer for the services with the label `Services`
----
MATCH (package:Java:Package:ToplevelLayer)
WHERE package.name = 'services'
SET package:Services
RETURN package
----

[[arch:markServices]]
[source,cypher,role=concept,requiresConcepts="arch:markServiceLayer"]
.Labels each single service with the label `Service`
----
MATCH (serviceLayer:Services:HexArch)-[:CONTAINS]->(service:Package:Java)
WHERE service.name <> "common"
SET service:Service
return service
----

[[arch:markDomainLayer]]
[source,cypher,role=concept,requiresConcepts="arch:markTopLevelLayer"]
.Label the layer for the adapters with the label `Domains`
----
MATCH (package:Java:Package:ToplevelLayer)
WHERE package.name = 'domains'
SET package:Domains
RETURN package
----

[[arch:markDomains]]
[source,cypher,role=concept,requiresConcepts="arch:markDomainLayer"]
.Labels each single domain with the label `Domain` and `HexArch`
----
MATCH (domainLayer:Domains:HexArch)-[:CONTAINS]->(domain:Package:Java)
WHERE domain.name <> "common"
SET domain:HexArch:Domain
return domain
----

[[arch:markCommon]]
[source,cypher,role=concept,requiresConcepts="arch:markTopLevelLayer"]
.Labels each single common package inside each `ToplevelLayer` with the label `Common`
----
MATCH (:ToplevelLayer)-[:CONTAINS*]->(c:Package)
WHERE c.name = "common"
SET c:HexArch:Common
RETURN c
----

[[arch:markAllowedDependencies]]
[source,cypher,role=concept,requiresConcepts="arch:markDomainLayer,arch:markAdapterLayer,arch:markServiceLayer"]
.Label the allowed relationships between the layers
----
MATCH
  (adapterLayer:HexArch:Adapters),
  (domainLayer:HexArch:Domains),
  (serviceLayer:HexArch:Services)
MERGE
  (adapterLayer)-[:IS_ALLOWED_TO_USE]->(serviceLayer)
MERGE
  (serviceLayer)-[:IS_ALLOWED_TO_USE]->(domainLayer)
RETURN
  count(*) as Count
----

[[arch:markExistingRelations]]
[source,cypher,role=concept,requiresConcepts="arch:markDomainLayer,arch:markAdapterLayer,arch:markServiceLayer"]
.Label the real existing usage relationships
----
MATCH
  (l1:HexArch:ToplevelLayer)-[:CONTAINS*]->(t1:Type),
  (l2:HexArch:ToplevelLayer)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE
  l1 <> l2
WITH
  l1, l2, count(*) as weight
MERGE
  (l1)-[d:DEPENDS_ON_LAYER]->(l2)
SET
  d.weight = weight
RETURN
  l1 as Layer, l2 as LayerDependency, d as DependencyWeight
----

[[arch:detectIllegalLayerDependencies]]
[source,cypher,role=constraint,requiresConcepts="arch:markAllowedDependencies,arch:markExistingRelations"]
.Find all not allowed dependencies between the layers of the hexagonal architecture
----
MATCH
  (l1:ToplevelLayer)-[:DEPENDS_ON_LAYER]->(l2:ToplevelLayer)
WHERE NOT
  (l1:ToplevelLayer)-[:IS_ALLOWED_TO_USE]->(l2:ToplevelLayer)
WITH
  l1, l2
MATCH
  (l1)-[:CONTAINS*]->(t1:Type),
  (l2)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
RETURN
  l1 as Layer, t1 as Type, l2 as InvalidLayerDependency, collect(t2) as InvalidTypeDependencies
----

[[arch:detectIllegalDomainDependencies]]
[source,cypher,role=constraint,requiresConcepts="arch:markDomains"]
.Find all not allowed dependencies between the domains of the hexagonal architecture
----
MATCH
    (d1:HexArch:Domain)-[:CONTAINS*]->(t1:Type),
    (d2:HexArch:Domain)-[:CONTAINS*]->(t2:Type),
    (t1)-[:DEPENDS_ON]->(t2)
WHERE
    d1 <> d2
WITH
    d1, d2, (count(*) - 2) AS weight, collect(distinct t2) AS violations
RETURN
    d1 AS Domain, d2 AS DomainDependeny, weight AS DependencyWeight,
    violations AS InvalidTypeDependencies
----


[[arch:detectIllegalAdapterDependencies]]
[source,cypher,role=constraint,requiresConcepts="arch:markAdapters"]
.Find all not allowed dependencies between the adapters of the hexagonal architecture
----
MATCH
    (d1:HexArch:Adapter)-[:CONTAINS*]->(t1:Type),
    (d2:HexArch:Adapter)-[:CONTAINS*]->(t2:Type),
    (t1)-[:DEPENDS_ON]->(t2)
WHERE
    d1 <> d2
WITH
    d1, d2, (count(*) - 2) AS weight, collect(distinct t2) AS violations
RETURN
    d1 AS Adapter, d2 AS AdapterDependeny, weight AS DependencyWeight,
    violations AS InvalidTypeDependencies
----

[[arch:detectIllegalCommonDependencies]]
[source,cypher,role=constraint,requiresConcepts="arch:markCommon"]
.Find all not allowed dependencies for types contained in a `Common` package. They should not depend on types contained in packages other than `Common`, or outside same layer (latter checked via "arch:detectIllegalLayerDependencies" constraint)
----
MATCH 
  (c1:Common)-[:CONTAINS*]->(t1:Type)-[:DEPENDS_ON]->(t2:Type)
WHERE 
  (:ToplevelLayer)-[:CONTAINS*]->(t2)
AND NOT
  (:Common)-[:CONTAINS*]->(t2)
RETURN
  distinct(t1.fqn) as Type, t2.fqn as InvalidTypeDependency
----

// TODO Constraint Adapters exclude common package reported as violation if in different Adapter package
