// tag::structureDefault[]
[[structure:Default]]
[role=group,includesConstraints="structure:UndefinedComponentDependencies,structure:UnusedComponentDependencies,structure:CyclicComponentDependencies,structure:ApiMustNotDependOnImpl,structure:ComponentDependencyMustUseApi"]
== Structure

This document describes the rules regarding package dependencies.
// end::structureDefault[]

// tag::structureComponent[]
[[structure:Component]]
[source,cypher,role=concept]
.All packages in the root package of the main artifact are labeled as `Component`.
----
MATCH
  (:Main:Artifact)-[:CONTAINS]->(root:Package)-[:CONTAINS]->(component:Package)
WHERE
  root.fqn = "your.project"
SET
  component:Component
RETURN
  component as Component
ORDER BY
  component.name desc
----
// end::structureComponent[]

// tag::structureDefinedComponentDependencies[]
[[structure:DefinedComponentDependencies]]
[source,cypher,role=concept,requiresConcepts="structure:Component",verify=aggregation]
.Allowed dependencies between components are represented by relationships of type `DEFINES_COMPONENT_DEPENDENCY`.
----
MATCH
  (a:Component{name:"a"}),
  (b:Component{name:"b"}),
  (c:Component{name:"c"})
MERGE
  (b)-[:DEFINES_COMPONENT_DEPENDENCY]->(a)
MERGE
  (c)-[:DEFINES_COMPONENT_DEPENDENCY]->(b)
MERGE
  (c)-[:DEFINES_COMPONENT_DEPENDENCY]->(a)
RETURN
  count(*) as Count
----
// end::structureDefinedComponentDependencies[]

// tag::structureComponentDependencies[]
[[structure:ComponentDependencies]]
[source,cypher,role=concept,requiresConcepts="structure:Component"]
.A component depends on another component if there is a dependency on type level between both. These dependencies are represented by relationships of type `DEPENDS_ON_COMPONENT`.
----
MATCH
  (c1:Component)-[:CONTAINS*]->(t1:Type),
  (c2:Component)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE
  c1 <> c2
WITH
  c1, c2, count(*) as weight
MERGE
  (c1)-[d:DEPENDS_ON_COMPONENT]->(c2)
SET
  d.weight = weight
RETURN
  c1 as Component, c2 as ComponentDependency, d as Dependency
----
// end::structureComponentDependencies[]

// tag::structureUndefinedComponentDependencies[]
[[structure:UndefinedComponentDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:DefinedComponentDependencies,structure:ComponentDependencies"]
.Dependencies between components are only allowed if there is a defined dependency between them.
----
MATCH
  (c1:Component)-[:DEPENDS_ON_COMPONENT]->(c2:Component)
WHERE NOT
  (c1:Component)-[:DEFINES_COMPONENT_DEPENDENCY]->(c2:Component)
WITH
  c1, c2
MATCH
  (c1)-[:CONTAINS*]->(t1:Type),
  (c2)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
RETURN
  c1 as Component, t1 as Type, c2 as InvalidComponentDependency, collect(t2) as InvalidTypeDependencies
----
// end::structureUndefinedComponentDependencies[]

// tag::structureUnusedComponentDependencies[]
[[structure:UnusedComponentDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:DefinedComponentDependencies,structure:ComponentDependencies"]
.There must be not defined but unused dependencies between components.
----
MATCH
  (c1:Component)-[:DEFINES_COMPONENT_DEPENDENCY]->(c2:Component)
WHERE NOT
  (c1:Component)-[:DEPENDS_ON_COMPONENT]->(c2:Component)
RETURN
  c1 as Component, c2 as UnusedDependency
----
// end::structureUnusedComponentDependencies[]

// tag::structureCyclicComponentDependencies[]
[[structure:CyclicComponentDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:ComponentDependencies"]
.There must be no cyclic dependencies between components.
----
MATCH
  (c1:Component)-[:DEFINES_COMPONENT_DEPENDENCY]->(c2:Component),
  cycle=shortestPath((c2)-[:DEFINES_COMPONENT_DEPENDENCY*]->(c1))
RETURN
  c1 as Component, nodes(cycle) as Cycle
----
// end::structureCyclicComponentDependencies[]

// tag::structureApi[]
[[structure:Api]]
[source,cypher,role=concept,requiresConcepts="structure:Component"]
.API classes of a component are located in the package `api` directly located within the component. This package is labeled with `Api`.
----
MATCH
  (component:Component)-[:CONTAINS]->(api:Package)
WHERE
  api.name = "api"
SET
  api:Api
RETURN
  component as Component, api as Api
----
// end::structureApi[]

// tag::structureImpl[]
[[structure:Impl]]
[source,cypher,role=concept,requiresConcepts="structure:Component"]
.Implementation classes of a component are located in the package `impl` located within the component. This package is labeled with `Impl`.
----
MATCH
  (component:Component)-[:CONTAINS]->(impl:Package)
WHERE
  impl.name = "impl"
SET
  impl:Impl
RETURN
  component as Component, impl as Impl
----
// end::structureImpl[]

// tag::structureApiMustNotDependOnImpl[]
[[structure:ApiMustNotDependOnImpl]]
[source,cypher,role=constraint,requiresConcepts="structure:Api,structure:Impl"]
.API types must not depend on implementation types of a component.
----
MATCH
  (component:Component),
  (component)-[:CONTAINS]->(:Api)-[:CONTAINS*]->(api:Type),
  (component)-[:CONTAINS]->(:Impl)-[:CONTAINS*]->(impl:Type),
  (api)-[:DEPENDS_ON]->(impl)
RETURN
  component as Component, api as ApiType, impl as ImplType
----
// end::structureApiMustNotDependOnImpl[]

// tag::structureComponentDependencyMustUseApi[]
[[structure:ComponentDependencyMustUseApi]]
[source,cypher,role=constraint,requiresConcepts="structure:Api,structure:Impl,structure:ComponentDependencies"]
.Only types provided by a component API may be used as dependency by another component.
----
MATCH
  (c1:Component)-[:DEPENDS_ON_COMPONENT]->(c2:Component),
  (c1)-[:CONTAINS*]->(t1:Type),
  (c2)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE NOT
  (:Api)-[:CONTAINS*]->(t2)
RETURN
  c1 as Component, t1 as Type, c2 as ComponentDependency, t2 as InvalidTypeDependency
----
// end::structureComponentDependencyMustUseApi[]
